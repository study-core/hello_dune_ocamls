(* 
********************************************************************
********************************************************************
********************************************************************
********************************************************************
********************************************************************
********************************************************************   

实例变量的所有成员都是  【私有的】 

********************************************************************
********************************************************************
********************************************************************
********************************************************************
********************************************************************
******************************************************************** 
*)



(* 
  class 的类型:    

------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
  (即，可以在  mli 文件中 的定义: class point : int -> int -> object .. end, 在  ml 文件中 的定义: class point ini_x ini_y = object .. end )

  class point :
    int ->
    int ->
    object
      val mutable x : int
      val mutable y : int
      method private print_x : unit
      method set : int -> int -> unit
    end  

------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------    
    
*)
class point ini_x ini_y =

    object (self)  (* self  this 随便命名 *)
      val mutable x = 0 (* 实例变量不能从外部访问 *)
      val mutable y = 0

      (*
       * 实例方法
       * method 方法名称 参数... = 表达式
       *)
      (* method set new_x new_y = begin x <- new_x; y <- new_y end *)
      method set new_x new_y = (x <- new_x; y <- new_y)
      method private print_x = print_int x (* 私有方法 *)

      (* 构造函数 *)
      initializer (
        x <- ini_x; y <- ini_y
      )
    end;;


(* 
      val p : point = <obj>
*)
let p = new point 0 0;;


(* - : unit = () *)
p#set 1 2;;



(* 
######################################################################################################################################################
######################################################################################################################################################
######################################################################################################################################################

继 承

######################################################################################################################################################
######################################################################################################################################################
######################################################################################################################################################
*)


(* 

    class 的类型: 

------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------ 

    (即，可以在  mli 文件中 的定义: class point_with_print : int -> int -> object .. end, 在  ml 文件中 的定义: class point_with_print ini_x ini_y = object .. end )

    class point_with_print :
      int ->
      int ->
      object
        val mutable x : int
        val mutable y : int
        method print : unit
        method private print_x : unit
        method set : int -> int -> unit
      end

------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------

*)

class point_with_print x y =
    object (self) (* 此处也可以只写 object , 因为在成员中没有用到 self# 之类的语法 *)
      inherit point x y as super (* 访问父类的名称 *)
      method print = Printf.printf "(%d, %d)\n" x y
    end;;


(* 
    val p : point_with_print = <obj>
*)
let p = new point_with_print 1 1;;

(* - : unit = () *)
p#print;;



(* 
######################################################################################################################################################
######################################################################################################################################################
######################################################################################################################################################

Object 类型   用于直接定义 object  类似 scala 中的 object

######################################################################################################################################################
######################################################################################################################################################
######################################################################################################################################################
*)



(* 

------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------

可以使用 object 代替 record   【 object和 record 对比 】

------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------







class 不能使用 【类型递归定义】, 

但 object 的类型与 record 类型非常相似【可以递归定义】，并且可以在类型定义中使用 (意思是，可以和相同 签名的 class 类型匹配 【下面就有讲 unrelated_class】)。

此外，可以在没有类的情况下创建对象。它们被称为 【直接对象】 

*)


(* 
    
    直接定义 object， 语法等同 class 的 = 号后面的定义

    val obj : < set : int -> int -> unit > = <obj>    


    该类型仅由其公共方法定义。值不可见，私有方法也不可见（未显示）。与记录不同，这种类型不需要  显式预定义。  【查看  object  和 record 区别】


    ********************************************************************
    注意: 具有满足这个定义的方法 的 class 被认为是 相同的 对象类型
    ********************************************************************
*)
let obj = 
  object (self)
    val mutable x = 0
    val mutable y = 0
    method set new_x new_y = (x <- new_x; y <- new_y)
  end;;


(* - : unit = () *)
obj#set 1 2;;


(* 
********************************************************************  
对比下这句话: 

具有满足这个定义的方法 的 class 被认为是 相同的 对象类型

class 类型不是 【数据类型】，称为【类型】。 object 类型是一种【数据类型】，就像记录类型或元组一样。


【定义 class 时，同时定义了同名的 class 类型和 object 类型】


首先，我们定义上面与obj无关的类 
********************************************************************
*)
(* 
    class unrelated_class : object method set : int -> int -> unit end   
*)
class unrelated_class =
    object
      (* 定义一个显示x，y的方法集 *)
      method set x y = Printf.printf "(%d, %d)\n" x y
    end;;



(* 
    由于对象类型匹配，它们 【可以】被放在同一个列表中
    val obj2 : unrelated_class = <obj>
*)
let obj2 = new unrelated_class;;


(* 
      - : unrelated_class list = [<obj>; <obj>]

      ********************************************************************
      直接定义的 obj  和 使用 class 定义的 obj2 由于类型一致:
      ********************************************************************

      ********************************************************************
      都具备 方法:  set : int -> int -> unit end   
      ********************************************************************

      故被认为是相同的对象类型
      可以放到同一个 list 中
*)
let lis = [obj; obj2];;  (* unrelated_class list *)


(* 
    由于对象类型匹配，可以将其作为相同的返回值类型进行处理

    val hoge : bool -> unrelated_class = <fun>
*)
let hoge x = if x then obj else new unrelated_class;;

(* 
   等同  obj#set
    - : unit = ()
*)
(hoge true)#set 1 2;;

(* 
      等同 unrelated_class#set
      (1, 2)
      - : unit = ()
*)
(hoge false)#set 1 2;;


(* 
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------

【object和record对比】

------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------

我们可以这样做：
*)

type counter = <get : int; incr : unit>;;   (* 【object 类型定义】    type counter = < get : int; incr : unit > *)
type counter_r = {get : unit -> int; incr : unit -> unit};;  (* 【record 类型定义 】    type counter_r = { get : unit -> int; incr : unit -> unit; } *)


let r =
  let n = ref 0 in
    {get = (fun () -> !n);
     incr = (fun () -> incr n)};;   (* 实例化一个 record; 像 object 一样工作的记录的实现       val r : counter_r = {get = <fun>; incr = <fun>} *)

(* 

【可以使用 object 代替 record 】、【 object和 record 对比 】

object  和 record 都很相似，但每种解决方案都有自己的优点： 

1、 速度： record 中的字段访问稍快一些

2、 字段名称：当某些字段名称相同时，操作不同类型的 record 会很不方便 【需要用到  多态变体】，但对于 object 来说这不是问题

3、 子类型化：不可能将 record 类型强制为 字段较少 的类型。 但对于 object 来说是可能的，因此 【共享某些方法】 的 【不同类型】 的 object 可以混合在一个数据结构中，其中只有它们的 【公共方法】 可见

4、 类型定义：不需要预先定义 object 类型，因此减轻了模块之间的依赖约束  (record 类型需要预先定义)


【结论】：  record 访问快，  object 使用方便且实用
*)




(* 
######################################################################################################################################################
######################################################################################################################################################
######################################################################################################################################################

 部分 类型

######################################################################################################################################################
######################################################################################################################################################
######################################################################################################################################################
*)


(* 
********************************************************************
********************************************************************  
******************************************************************** 
print_class1 是 print_class2 的部分类型 
********************************************************************
********************************************************************
********************************************************************

原因， print_class2  的所有方法集 包含 print_class1 的所有方法集
*)

(* 
      class print_class1 : object method print_1 : unit end
*)
class print_class1 = object
  method print_1 = print_int 1
end;;



(* 
    class print_class2 : object method print_1 : unit method print_2 : unit end   
*)
class print_class2 = object
  method print_1 = print_int 1
  method print_2 = print_int 2
end;;


(* 
********************************************************************   
由于对象类型不同，print_class 1 和 print_class 2 【不能】放在 同一个列表中 
********************************************************************
*)
(* 
    Error: This expression has type print_class2
           but an expression was expected of type print_class1
           The second object type has no method print_2   
*)
let obj_list = [new print_class1; new print_class2];;



(* 
********************************************************************
    通过指导（类型转换）把它们放在同一个列表中          【强转】 语法

    【子类 :> 父类】


    【注意】 父类 :> 子类 是不允许的



    A :> B ==> A 实例 转  B 实例           
********************************************************************    
*)
(* 
    val obj_list : print_class1 list = [<obj>; <obj>]   
*)
let obj_list = [new print_class1; (new print_class2 :> print_class1)];;


(* 
********************************************************************   
子类被转化为 父类后的 引用不可以继续调用 子类特有的方法  (因为被擦除了)
********************************************************************

    val obj1 : print_class1 = <obj>
    val obj2 : print_class1 = <obj>
*)

let [obj1; obj2] = obj_list;;   (* 类似 rust list 解构 ??? *)


(* 1- : unit = () *)
obj1#print_1;; (* 可以调用 【父类方法】 *)


(* 
      Error: This expression has type print_class1
      It has no method print_2   
*)
obj2#print_2;; (* 不能调用 【子类方法】，   因为 obj2 此时是一个 父类类型，没有子类方法*)




(* 
********************************************************************   
定义接受多层对象类型的函数  (这个 不是很明确， 略过)
********************************************************************

    val print1 : < print_1 : 'a; .. > -> 'a = <fun>   

    以上 < print_1 : 'a; .. > -> 'a = 中的 【-> 'a】 部分 'a 是一个【类型变量】 < print_1 : 'a; .. > 的别名
*)

let print1 print_obj = print_obj#print_1;;  (* 声明一个 全局的 print1 函数 传入  x 对象， 该函数要求入参的 x 具备  x#print_1 的调用*)


(* 1- : unit = () *)
print1 obj1;;

(* 1- : unit = () *)
print1 obj2;;



(* 
######################################################################################################################################################
######################################################################################################################################################
######################################################################################################################################################

多态类（具备泛型的类）

######################################################################################################################################################
######################################################################################################################################################
######################################################################################################################################################
*)


(* 
    class 的类型: 

------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------

    class ['a] stack :
      object
        val mutable list : 'a list
        method peek : 'a
        method pop : 'a
        method push : 'a -> unit
        method size : int
      end   

------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------      
*)

class ['a] stack =

    object (self)
      val mutable list = ( [] : 'a list )  (* 没办法 有 list 只能声明   'a   泛型啦 *)

      method push x = list <- x :: list    (* 推入堆栈 *)

      method pop =                         (* 从堆栈中移除(pop) *)
        let result = List.hd list in
        list <- List.tl list;
        result

      method peek = List.hd list     (* 堆栈峰值 *)

      method size = List.length list (* 堆栈的大小 *)

    end;;




(* 
######################################################################################################################################################
######################################################################################################################################################
######################################################################################################################################################

抽象类

######################################################################################################################################################
######################################################################################################################################################
######################################################################################################################################################
*)

(* 
    class 的类型: 

------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------

class virtual widget1 :
  string -> object method get_name : string method virtual repaint : unit end

------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------------------------------------------  
*)

class virtual widget1 (name : string) =
  object (self)
    method get_name =
      name
    method virtual repaint : unit
  end;;

(* 
class virtual container :
  string ->
  object
    val mutable widgets : widget1 list
    method add : widget -> unit
    method get_name : string
    method get_widgets : widget1 list
    method repaint : unit
  end   
*)
class virtual container name =
  object (self)
    inherit widget1 name
    val mutable widgets = ([] : widget1 list)
    method add w =
      widgets <- w :: widgets
    method get_widgets =
      widgets
    method repaint =
      List.iter (fun w -> w#repaint) widgets
  end;;

type button_state = Released | Pressed;;

(* 
class button :
  ?callback:(unit -> unit) ->
  string ->
  object
    val mutable state : button_state
    val mutable widgets : widget list
    method add : widget -> unit
    method get_name : string
    method get_widgets : widget list
    method press : unit
    method release : unit
    method repaint : unit
  end   
*)
class button ?callback name =
  object (self)
    inherit container name as super
    val mutable state = Released
    method press =
      state <- Pressed;
      match callback with
      | None -> ()
      | Some f -> f ()
    method release =
      state <- Released
    method repaint =
      super#repaint;
      print_endline ("Button being repainted, state is " ^
                     (match state with
                      | Pressed -> "Pressed"
                      | Released -> "Released"))
  end;;


let b = new button ~callback:(fun () -> print_endline "Ouch!") "button";;   (* val b : button = <obj> *)

b#repaint;;   (* Button being repainted, state is Released *)



b#press;;  (* Ouch! *)


b#repaint;; (* Button being repainted, state is Pressed *)


(* 
class label :
  string ->
  string -> object method get_name : string method repaint : unit end   
*)
class label name text =
  object (self)
    inherit widget1 name
    method repaint =
      print_endline ("Label: " ^ text)
  end;;

let l = new label "label" "Press me!";;   (* val l : label = <obj> *)
  
b#add l;;


(* 
Label: Press me!
  Button being repainted, state is Released   
*)
b#repaint;;    
  

let b = new button "button";;    (* val b : button = <obj> *)

let l = new label "label" "Press me!";;   (* val l : label = <obj> *)


let wl = ([] : widget1 list);;    (* 声明一个 父类类型的 list      val wl : widget list = [] *)


(* 
将 子类 转成 父类，再加入 list 中   
*)
let wl = (b :> widget1) :: wl;;    (* val wl : widget1 list = [<obj>] *)
let wl = (l :> widget1) :: wl;;    (* val wl : widget1 list = [<obj>; <obj>] *)




(* m面向对象中的  “只认方法”  的多态做法*)

(* 这里的 s 没有指定具体的类名，编译器推断它是一个“拥有 draw 方法”的对象 *)
let render s = s#draw ;;

(* 定义两个完全没有继承关系的类 *)
class circle = object 
  method draw = print_endline "Circle" 
end

class square = object 
  method draw = print_endline "Square" 
end

(* 都可以传进去！这就是“只认方法不认名字” *)
let () =
  render (new circle);
  render (new square);
;;
(* 
和 多态变体 、函子、第一类模块 等实现多态时的不足：

1. 模式匹配（Pattern Matching）的缺失
  这是最致命的原因。在函数式编程中，模式匹配是灵魂。
        变体（Variant）：你可以轻松地通过 match 拆解一个变体，根据它的不同形态（构造子）写出不同的逻辑。
        对象（Object）：对象是一个“黑盒”。你只能调用它的方法，无法通过模式匹配来观察它的内部结构。在处理复杂的数据转换逻辑时，对象显得非常笨拙。

2. 性能开销（Performance Overheads）
  OCaml 的对象系统在性能上是有代价的：
        方法查找（Method Lookup）：对象的方法调用（如 s#draw）是通过哈希表查找或动态分发实现的。这比普通函数的直接跳转或变体类型的索引跳转要慢。
        单态化困难：编译器更难对对象代码进行内联（Inline）优化。相比之下，函子在编译时就会生成具体代码，几乎没有运行时开销。

3. 类型推断的“爆炸”与报错信息
  对象的类型推断虽然强大，但产生的签名非常复杂。
    对象签名：< draw : unit; .. >。当对象嵌套多层时，那个 .. 会携带大量的类型约束，导致编译报错信息极其冗长且难以读懂。
    多态变体：虽然也是结构化的，但其标签（Tag）逻辑更扁平，报错信息通常比对象系统直观得多。

4. 显式（Explicit）优于隐式（Implicit）
  在金融（Jane Street）或区块链（Mina）这类对安全性要求极高的领域，开发者倾向于显式的契约：
    函子（Functor）：明确要求“输入模块必须满足 X 接口”。
    第一类模块：明确解包并约束 with type t = ...。
    对象系统的“鸭子类型”太隐式了。如果你不小心传错了一个方法名完全相同但逻辑不同的对象，编译器可能不会报错，这在底层协议开发中是巨大的风险。
5. 状态管理与不可变性
    类/对象：天然地暗示了“内部状态（State）”和“副作用（Side Effects）”。
    变体/模块：天然地鼓励“数据与逻辑分离”以及“不可变数据结构”。这更符合 OCaml 社区倡导的函数式编程范式，使得代码更容易测试和进行形式化验证。




总结：什么时候该选什么？

|  多态实现方式	     | 核心优势	                   |  适用场景                                |
|-------------------|----------------------------|------------------------------------------|
|  标准/多态变体	   | 极其强大的模式匹配能力	      | 核心业务逻辑、状态机、数据转换             |
|  函子 (Functors)	| 零运行时开销，极其严谨的抽象	| 建立通用库（如 Set/Map）、系统底层架构      |
|  第一类模块	      | 运行时动态性 + 严格类型约束	  | 策略模式、动态插件加载、依赖注入           |
|  对象与类 (OO)	  | 极其灵活的结构化多态	       | GUI 组件、模拟 JS 对象、处理极多方法的 DOM |

最终结论：

在 OCaml 中，Class 被视为一种“逃生舱口”。
只有当 Module 系统（哪怕是第一类模块）处理起来过于繁琐（例如有 50 个方法需要层层传递）时，
开发者才会考虑使用 Class。
在项目中，“数据驱动”（变体）远比“行为驱动”（对象）更受青睐。
*)